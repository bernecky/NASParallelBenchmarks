use StdIO: all;
use Math: all;
use Array: all;

/*
 * The following defines are needed for "telling" the compiler 
 * all possible input sizes. For more information see "main".
 */

#define S0 4
#define S1 6
#define S2 10
#define S3 18
#define S4 34
#define S5 66
#define S6 130
#define S7 258


/*
 * The following flags are used to create different versions:
 * DISTRIB_LAW_BUG_IS_FIXED indicates whether the compiler is capable
 *   to apply distributive law (1) or not (0).
 * USE_SAME_SHORTCUT_AS_IN_FORTRAN indicates whether the algorithm patch
 *   used in the Fortran version of the benchmark in order to spare one
 *   copy of the largest array should be applied in SAC as well (1) or not (0).
 * MINIMIZE_ADDITIONS chooses whether to pre-compute vectors of sums of neighbor
 *   elements in order to share these additions among the computations of
 *   several different elements. If set to non-zero, the number of additions per element
 *   decreases from 27 to 16. There are two versions available using 16 additions:
 *   The first version (1) tries to store the pre-computed values in vectors in order to
 *   keep the memory traffic as small as possible. Unfortunately, this requires a
 *   nested WL to be used whose compilation leads to relatively poor C-code.
 *   As a consequence, at the time being (May 3 2000) this leads to a 
 *   runtime slowdown of about factor 2 8-((.
 *   The second version (2) circumvents the usage of nested WLs by using 3D arrays
 *   for the pre-computed values. Due to the increase in memory traffic this leads
 *   also to a slowdown when compared against the version with 27 additions (0).
 *   Surprisingly enough, the slowdown factor for this version is about 1.7....
 *   All these numbers where gained using gcc on a single processor machine....
 */

#define DISTRIB_LAW_BUG_IS_FIXED 0
#define USE_SAME_SHORTCUT_AS_IN_FORTRAN 1

#ifndef MINIMIZE_ADDITIONS
#define MINIMIZE_ADDITIONS 0
#endif

/* 
 * Now, we define the arrays of weights needed for relaxation (A),
 * restriction (P), extension (Q), and smoothing (S).
 * In the NAS-MG benchmark, all these have a particular property:
 * weights with the same "distance" from the "center" do have the same
 * value! Therefore, in the 3-dimensional case, they can be described
 * by a vector containing 4 values: 
 * [ center, distance 1, distance 2, distance 3]
 *
 * Another consequence of this property is that for the computation
 * of the required weighted sums the distributive law is applicable,
 * which leads to a noticable speedup in program execution.
 * Since the FORTRAN program is hand-optimized to this effect,
 * we have to make sure that the distributive law optimization
 * of the SAC compiler is turned on!
 */

#define AP [ -8d/3d, 0d    , 1d/6d  , 1d/12d]
#define PP [ 1d/2d , 1d/4d , 1d/8d  , 1d/16d]
#define QP [ 1d    , 1d/2d , 1d/4d  , 1d/8d ]
#define SP [ -3d/8d, 1d/32d, -1d/64d, 0d    ]

#define SHP [3,3,3]

#if DISTRIB_LAW_BUG_IS_FIXED

#define A gen_weights( AP)
#define P gen_weights( PP)
#define Q gen_weights( QP)
#define S gen_weights( SP)

inline double[+] gen_weights( double[+] wp)
{
  res = with {
          ( . <= iv <= . )
          {
            off = with {
                    ( 0*shape(iv) <= x < shape(iv) )
                    {
                      if ( iv[ix] != 1 )
                        dist = 1;
                      else
                        dist = 0;
                    } : dist;
                  } : fold( +, 0 );
          } : wp[[off]];
        } : genarray( SHP );

  return( res);
}
#else

/*
 * Since the actual SAC2C compiler due to a bug does not support
 * distributive law optimization, a modified version of the function
 * that computes the weighted sums ("weighted_sum") has to be used
 * which is hand optimized to that effect.
 * As a consequence, A, P, Q, and S are not arrays of weights of shape
 * SHP, but are only the carriers of different values needed for the
 * particularities of the NAS-MG benchmark.
 */

#define A AP
#define P PP
#define Q QP
#define S SP

#endif


double l2norm_inner( double[+] u)
{
  sqrsum = with {
             ( 0*shape(u)+1 <= x < shape(u)-2 ) : u[x]*u[x];
           } : fold( +, 0d );
  n_elem = with {
             ( [0] <= y < [dim(u)-1] ) : shape(u)[y] -2;
           } : fold( *, 1 );

  return( sqrt( sqrsum/tod(n_elem)));
}

double[+] setup_periodic_border( double[+] u)
{
  for( x=1; x<shape(u)[0]-1; x++) {
    for( y=1; y<shape(u)[1]-1; y++) {
      z = shape(u)[2];
      u = modarray( u, [x,y,0], u[[x,y,z-2]]);
      u = modarray( u, [x,y,z-1], u[[x,y,1]]);
    }
  }
 
  for( x=1; x<shape(u)[0]-1; x++) {
    y = shape(u)[1];
    for( z=0; z<shape(u)[2]; z++) {
      u = modarray( u, [x,0,z], u[[x,y-2,z]]);
      u = modarray( u, [x,y-1,z], u[[x,1,z]]);
    }
  }
 
  x = shape(u)[0];
  for( y=0; y<shape(u)[0]; y++) {
    for( z=0; z<shape(u)[2]; z++) {
      u = modarray( u, [0,y,z], u[[x-2,y,z]]);
      u = modarray( u, [x-1,y,z], u[[1,y,z]]);
    }
  }
 
  return(u);
}




#if DISTRIB_LAW_BUG_IS_FIXED

inline double weighted_sum( double[+] u, int[+] x, double[+] w)
{
  res = with {
          ( 0*shape(w) <= dx < shape(w) ) : u[x+dx-1] * w[dx];
        } : fold( +, 0d );

  return(res);
}

#else
inline double weighted_sum( double[+] u, int[3] x, double[4] w)
{
  return(  w[0] * u[x]
         + w[1] * ( u[x+[1,0,0]] + u[x-[1,0,0]]
                    + u[x+[0,1,0]] + u[x-[0,1,0]]
                    + u[x+[0,0,1]] + u[x-[0,0,1]] )
         + w[2] * ( u[x+[1,1,0]] + u[x-[1,1,0]]
                    + u[x+[0,1,1]] + u[x-[0,1,1]]
                    + u[x+[1,0,1]] + u[x-[1,0,1]]
                    + u[x+[-1,1,0]] + u[x-[-1,1,0]]
                    + u[x+[0,-1,1]] + u[x-[0,-1,1]]
                    + u[x+[-1,0,1]] + u[x-[-1,0,1]] )
         + w[3] * ( u[x+[1,1,1]] + u[x-[1,1,1]]
                    + u[x+[-1,1,1]] + u[x-[-1,1,1]]
                    + u[x+[1,-1,1]] + u[x-[1,-1,1]]
                    + u[x+[1,1,-1]] + u[x-[1,1,-1]] ) );
}
#endif


#if (MINIMIZE_ADDITIONS == 1)
inline double[+] weighted_sum_opt( double[+] u, double[+] tmp, double[+] tmp2, double[4] w)
{
  return(  w[0] * u
         + w[1] * ( tmp + shift( 0, 1, 0d, u) + shift( 0, -1, 0d, u))
         + w[2] * ( tmp2 + shift( 0, 1, 0d, tmp) + shift( 0, -1, 0d, tmp))
         + w[3] * ( shift( 0, 1, 0d, tmp2) + shift( 0, -1, 0d, tmp2)) );
}

double[+], double[+] pre_compute( double[+] u, int i, int j)
{
  tmp = with {
          ( . <= k <= . ) : ( u[i+1, j] + u[i-1, j] + u[i, j+1] + u[i, j-1] );
        } : genarray( drop( [2], shape(u) ) );
        
  tmp2 = with {
           ( . <= k <= . ) : ( u[i-1,j-1] + u[i-1, j+1] + u[i+1, j+1] + u[i+1, j-1] );
         } : genarray( drop( [2], shape(u) ) );

  return( tmp, tmp2);
}

inline double[+] NullifyBorders( double[+] a)
{
  res = with {
          ( . < iv < . ) : a[iv];
        } genarray( shape(a) );

  return( res);
}

inline double[+] relax_kernel( double[+] w, double[+] u)
{
  r = with {
        ( take( [2], 0*shape(u)+1) <= x=[i,j] <= take( [2], shape(u)-2) )
        {
          tmp, tmp2 = pre_compute( u, i, j);
          new_vect  = weighted_sum_opt( u[[i,j]], tmp, tmp2, w);
        } : NullifyBorders( new_vect );
      } : modarray( u );

  return(r);
}

#elif (MINIMIZE_ADDITIONS == 2)

inline double weighted_sum_opt( double[+] u, double[+] tmp, double[+] tmp2,
                                                           int[+] x, double[4] w)
{
  return(  w[0] * u[x]
         + w[1] * ( tmp[x] + u[x+[0,0,1]] + u[x-[0,0,1]])
         + w[2] * ( tmp2[x] + tmp[x+[0,0,1]] + tmp[x-[0,0,1]])
         + w[3] * ( tmp2[x+[0,0,1]] + tmp2[x-[0,0,1]] ));
}

double[+], double[+] pre_compute( double[+] u)
{
  tmp = with {
          ( [1,1,0] <= x <= shape(u)-2+[0,0,1] )
          : u[x+[1,0,0]] + u[x-[1,0,0]] + u[x+[0,1,0]] + u[x-[0,1,0]];
        } : genarray( shape(u) );

  tmp2 = with {
           ( [1,1,0] <= x <= shape(u)-2+[0,0,1] )
           : u[x+[1,1,0]] + u[x-[1,1,0]] + u[x+[1,-1,0]] + u[x-[1,-1,0]];
         } : genarray( shape(u) );

  return( tmp, tmp2);
}

inline double[+] relax_kernel( double[+] w, double[+] u)
{
  tmp, tmp2 = pre_compute( u);
  
  r = with {
        ( 0*shape(u)+1 <= x <= shape(u)-2 )
        : weighted_sum_opt( u, tmp, tmp2, x, w);
      } : modarray( u );

  return(r);
}

#else
inline double[+] relax_kernel( double[+] w, double[+] u)
{
  r = with {
        ( 0*shape(u)+1 <= x <= shape(u)-2 ) : weighted_sum( u, x, w );
      } : modarray( u );

  return(r);
}
#endif

double[+] relax( double[+] u, double[+] v)
{
  r = v - relax_kernel( A, u);
  r = setup_periodic_border(r);
  return(r);
}

double[+] smooth( double[+] z, double[+] r)
{
  z = z + relax_kernel( S, r);
  z = setup_periodic_border( z);
  return( z);
}

double[+] smooth( double[+] r)
{
  z = relax_kernel( S, r);
  z = setup_periodic_border( z);
  return( z);
}

double[+] fine2coarse( double[+] r)
{
  rn = with {
         ( 0*shape(r)+1 <= x <= shape(r) / 2 -1 )
         : weighted_sum( r, 2*x, P );
       } : genarray( shape(r) / 2 + 1, 0d );

  rn = setup_periodic_border(rn);
  return(rn);
}

double[+] coarse2fine( double[+] rn)
{
  r = with {
        ( 0*shape(rn) <= iv <= 2*shape(rn)-3 step 0*shape(rn)+2 ) : rn[iv/2];
      } : genarray( 2*shape(rn)-2, 0d );
  
  r = relax_kernel( Q, r);
  r = setup_periodic_border(r);
  return(r);
}



double[+] mgrid( double[+] r)
{ /*
   * non-coarsest grid: r(k-1) = P r(k)			(fine2coarse)
   *                    z(k-1) = M(k-1) r(k-1)		(recursive solve)
   *                    z(k)   = Q z(k-1)		(coarse2fine)
   *                    r(k)   = r(k) - A z(k)		(relax)
   *                    z(k)   = z(k) + S r(k)          (smooth)
   */
  rn = fine2coarse( r);
  zn = mgrid( rn);
  z  = coarse2fine( zn);
  r  = relax( z, r);
  z  = smooth( z, r);

  return(z);
}

double[S0,S0,S0] mgrid( double[S0,S0,S0] r)
{
  z = smooth( r);
  return( z);
}

specialize
double nas_mgrid( double[S0, S0, S0] u, double[S0, S0, S0] v,
                  int [10, 3] pos     , int[10, 3] neg, int iter );
specialize
double nas_mgrid( double[S1, S1, S1] u, double[S1, S1, S1] v,
                  int [10, 3] pos     , int[10, 3] neg, int iter );
specialize
double nas_mgrid( double[S2, S2, S2] u, double[S2, S2, S2] v,
                  int [10, 3] pos     , int[10, 3] neg, int iter );
specialize
double nas_mgrid( double[S3, S3, S3] u, double[S3, S3, S3] v,
                  int [10, 3] pos     , int[10, 3] neg, int iter );

double nas_mgrid( double[+] u, double[+] v, int[+] pos, int[+] neg, int iter)
{
  for( i=0; i<10; i++)
    v = modarray( v, neg[[i]], -1d);

  for( i=0; i<10; i++)
    v = modarray( v, pos[[i]], 1d);

  v = setup_periodic_border(v);

  r = relax( u, v);

  i = 0;
  
  do {
    
#if USE_SAME_SHORTCUT_AS_IN_FORTRAN
    rn = fine2coarse(r);
    zn = mgrid(rn);
    u  = u + coarse2fine(zn);
    r  = relax(u, v);
    u  = smooth(u, r);
#else
    u  = u + mgrid( r);
#endif

    r  = relax( u, v);
  
    i += 1;
  }
  while (i<iter);

  z = l2norm_inner( r);
  return(z);
}



/*
 * The tricky part of this program is that we want to runthe same
 * executable on different array sizes without re-compilation.
 * Since the current compiler only accepts programs where all
 * shapes involved can be determined statically, we have to use
 * a nesting of conditionals in order to force the compiler
 * to generate the variants we are interested in.
 */
int main()
{
  int[10,3] pos;
  int[10,3] neg;

  size = (int) FibreScanIntArray();
  iter = (int) FibreScanIntArray();
  pos  = (int[10, 3]) FibreScanIntArray();
  neg  = (int[10, 3]) FibreScanIntArray();

  if(size == 256) {
    u_256 = with {
              ( [0,0,0] <= x <= [S7-1,S7-1,S7-1] ) : 0d;
            } : genarray( [S7,S7,S7] );
    
    v_256 = with {
              ( [0,0,0] <= x <= [S7-1,S7-1,S7-1] ) : 0d;
            } : genarray( [S7,S7,S7] );

    z = nas_mgrid(u_256, v_256, pos, neg, iter);
  }
  else{
    if(size == 128) {
      u_128 = with {
                ( [0,0,0] <= x <= [S6-1,S6-1,S6-1] ) : 0d;
              } : genarray( [S6,S6,S6] );

      v_128 = with {
                ( [0,0,0] <= x <= [S6-1,S6-1,S6-1] ) : 0d;
              } : genarray( [S6,S6,S6] );

      z = nas_mgrid(u_128, v_128, pos, neg, iter);
    }
    else {
      if(size == 64) {
        u_64 = with {
                 ( [0,0,0] <= x <= [S5-1,S5-1,S5-1] ) : 0d;
               } : genarray( [S5,S5,S5] );
        v_64 = with {
                 ( [0,0,0] <= x <= [S5-1,S5-1,S5-1] ) : 0d;
               } : genarray( [S5,S5,S5] );

        z = nas_mgrid(u_64, v_64, pos, neg, iter);
      }
      else{
        if(size == 32) {
          u_32 = with {
                   ( [0,0,0] <= x <= [S4-1,S4-1,S4-1] ) : 0d;
                 } : genarray( [S4,S4,S4] );
          v_32 = with {
                   ( [0,0,0] <= x <= [S4-1,S4-1,S4-1] ) : 0d;
                 } : genarray( [S4,S4,S4] );

          z = nas_mgrid(u_32, v_32, pos, neg, iter);
        }
        else {
          size = 0;
          iter = 0;
          z = 0d;
        }
      }
    }
  }

  printf( " L2-Norm after %d iteration(s) with max. grid [%d,%d,%d]: %.10e\n", 
          iter, size+2, size+2, size+2, z);

  return( 0);
}


